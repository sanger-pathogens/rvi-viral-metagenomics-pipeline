// Config inheritance options
params {
    generic_config_base = "https://raw.githubusercontent.com/sanger-pathogens/nextflow-commons/"
    generic_config_version = "master"
    generic_config_url = ""
}
inherit_generic_config()

manifest {
    name            = 'rvi-pipeline'
    author          = 'PAM Informatics'
    homePage        = 'https://gitlab.internal.sanger.ac.uk/sanger-pathogens/pipelines/rvi-pipeline'
    description     = 'Meta-pipeline for RVI project data analysis including all steps of shotgun metagenomic sample processing'
    mainScript      = 'main.nf'
    nextflowVersion = '>=21.04.0'
    version         = 'v1.2.2'
}

//import subworkflow default configs
includeConfig "./assorted-sub-workflows/irods_extractor/subworkflows/irods.config"
includeConfig "./assorted-sub-workflows/combined_input/subworkflows/combined_input.config"
includeConfig "./modules/preprocessing/preprocessing.config"
includeConfig "./modules/subsample/subsample.config"
includeConfig "./modules/kraken2bracken/kraken2bracken.config"
includeConfig "./modules/abundance_estimation/abundance_estimation.config"
includeConfig "./modules/assemble/assemble.config"

params {
    // general pipeline config
    monochrome_logs = false
    outdir = "./results"

    // LSF options
    queue_size = 50
    submit_rate_limit = '50/2min'

    // override iRODS Extractor default parameters
    preexisting_fastq_tag = "trimmed_reads"
    split_sep_for_ID_from_fastq = "_1_kneaddata_paired_1.fastq"
    cleanup_intermediate_files_irods_extractor = false
    save_fastqs = false
    save_method = "nested"
    lane_plex_sep = "#"

    // override metagenomic assembly default parameters 
    metaspades_base_mem_gb = 16

    // override Abundance estimation default parameters
    instrain_profile_options = "--min_genome_coverage 0 --min_read_ani 0.9" // RVI default; team162 would have "--database-mode"

    // override Kraken2Bracken default parameters 
    kraken2bracken_kraken2_db = "/data/pam/software/kraken2/viral_kraken/"
}

process {
    withName:KRAKEN2 {
        cpus = { params.kraken2bracken_kraken2_threads }
        memory = { estimate_kraken_mem(params.kraken2bracken_kraken2_db, 1, task) }
    }
}

// Helper functions
def inherit_generic_config() {
    config_url = params.generic_config_url ? params.generic_config_url : "${params.generic_config_base}/${params.generic_config_version}/configs/nextflow.config"
    try {
        includeConfig "${config_url}"
    } catch (Exception e) {
        System.err.println("ERROR: Could not load generic config: ${config_url}")
        System.err.println("Encountered the following exception:")
        throw e
    }
}

/*
Estimates required memory from kraken2 database files, accepts a memory top_up (in GB) to provide head room.
top_up will be increased linearly with each task attempt up to params.max_memory
*/
def estimate_kraken_mem(db, top_up, task) {
    float attempt = task.attempt
    float top_up_float = top_up
    def mem_file = new File("${db}/hash.k2d").size()
    def mem_top_up = (top_up_float * 1024**3) * attempt
    def mem_bytes = mem_file + mem_top_up
    def mem_mb = mem_bytes / (1024**2)
    def valid_mem = "${mem_mb.round()}.MB"
    if (valid_mem.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1) {
        return params.max_memory as nextflow.util.MemoryUnit
    }
    return valid_mem
}

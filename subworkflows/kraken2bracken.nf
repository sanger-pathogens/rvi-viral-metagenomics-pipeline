#!/usr/bin/env nextflow

/*
========================================================================================
    IMPORT MODULES/SUBWORKFLOWS
========================================================================================
*/

//
// MODULES
//
include { KRAKEN2; KRAKEN2_GET_CLASSIFIED; COMPRESS_READS } from '../modules/kraken2bracken/kraken2'
include { BRACKEN } from '../modules/kraken2bracken/bracken'
include { GENERATE_ABUNDANCE_SUMMARY } from '../modules/kraken2bracken/krakentools'

//
// SUBWORKFLOWS
//
include { BRACKEN_BUILD } from './bracken_build'

/*
========================================================================================
    RUN MAIN WORKFLOW
========================================================================================
*/

workflow KRAKEN2BRACKEN{

    take:
    ch_reads // meta, paired_reads

    main:

    Channel.fromPath(params.kraken2bracken_kraken2_db)
        .set { ch_kraken2_db }

    //
    // CLASSIFICATION
    //
    ch_reads
        .combine(ch_kraken2_db)
        .dump(tag: 'reads_and_kraken2_db')
        .set { ch_reads_and_kraken2_db }
    if (params.kraken2bracken_get_classified_reads) {
        KRAKEN2_GET_CLASSIFIED(ch_reads_and_kraken2_db)

        KRAKEN2_GET_CLASSIFIED.out.kraken2_sample_report.dump(tag: 'kraken2_sample_report').set { ch_kraken2_sample_report }
        KRAKEN2_GET_CLASSIFIED.out.classified_reads.dump(tag: 'kraken2_classified_reads').set { ch_kraken2_classified_reads }
        KRAKEN2_GET_CLASSIFIED.out.unclassified_reads.dump(tag: 'kraken2_unclassified_reads').set { ch_kraken2_unclassified_reads }
        ch_kraken2_classified_reads.join(ch_kraken2_unclassified_reads)
            .map { meta, classified, unclassified -> [meta, classified + unclassified] }
            .set { ch_reads_to_compress }

        COMPRESS_READS(
            ch_reads_to_compress
        )
    } else {
        KRAKEN2(ch_reads_and_kraken2_db)
        KRAKEN2.out.kraken2_sample_report.dump(tag: 'kraken2_sample_report').set { ch_kraken2_sample_report }
    }

    //
    // ABUNDANCE ESTIMATION
    //
    kraken2_db_dir = file(params.kraken2bracken_kraken2_db, checkIfExists: true)
    required_kmer_distrib = file("${kraken2_db_dir}/database${params.kraken2bracken_kmer_len}mers.kmer_distrib")
    if (!required_kmer_distrib.exists()) {
        BRACKEN_BUILD(
            ch_kraken2_db
        )
        BRACKEN_BUILD.out.ch_kmer_distrib
            .dump(tag: 'kmer_distrib')
            .set { ch_kmer_distrib }
    } else {
        Channel.fromPath(required_kmer_distrib)
            .dump(tag: 'kmer_distrib')
            .set { ch_kmer_distrib }
    }

    ch_kraken2_sample_report
        .combine(ch_kmer_distrib)
        .dump(tag: 'kraken2_report_and_kmer_distrib')
        .set { ch_kraken2_report_and_kmer_distrib }
    BRACKEN(
        ch_kraken2_report_and_kmer_distrib
    )

    //
    // SUMMARISE ABUNDANCE
    //
    BRACKEN.out.kraken_style_bracken_report
        .map { meta, report -> report }
        .collect()
        .dump(tag: 'kraken2_style_bracken_reports')
        .set { ch_kraken2_style_bracken_reports }
    GENERATE_ABUNDANCE_SUMMARY(
        ch_kraken2_style_bracken_reports
    )
}

/*
========================================================================================
    THE END
========================================================================================
*/
